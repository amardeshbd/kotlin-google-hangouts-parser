plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.kapt'
    id 'maven-publish'
}

group 'dev.hossain.hangouts'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation("com.squareup.moshi:moshi:${rootProject.moshiVersion}")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:${rootProject.moshiVersion}")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

/**
 * Allows the library files to be used in Android or other java project
 *
 * - https://jitpack.io/docs/BUILDING/
 * - https://docs.gradle.org/current/userguide/publishing_maven.html
 */
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'json-parser'


            pom {
                name = 'Hangouts JSON Parser'
                description = 'Google Hangouts takeout JSON file parser.'
                url = 'https://github.com/amardeshbd/kotlin-google-hangouts-parser'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/amardeshbd/kotlin-google-hangouts-parser/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'amardeshbd'
                        name = 'Hossain Khan'
                        email = 'eng@hossain.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/amardeshbd/kotlin-google-hangouts-parser.git'
                    developerConnection = 'scm:git:git@github.com:amardeshbd/kotlin-google-hangouts-parser.git'
                    url = 'https://github.com/amardeshbd/kotlin-google-hangouts-parser'
                }
            }
        }
    }
}